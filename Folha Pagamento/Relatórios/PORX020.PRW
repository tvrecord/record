#INCLUDE "RWMAKE.CH"
#INCLUDE "EXTRABH.CH"

/*


Ŀ
Funo     PORX020   Autor  Ana Claudia            Data  16.11.06 
Ĵ
Descrio  Apurao Mensal de Banco de Horas                          
Ĵ
Sintaxe    PORX020                                                   
Ĵ
Parametros                                                            
Ĵ
 Uso       Generico                                                   
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador  Data    Motivo da Alteracao                             
Ĵ
ٱ

*/
User Function PORX020()

Local aOrd		:= { 'Centro de Custo' , 'C.Custo + Nome'}
Local cDesc1    := 'Apurao Mensal de Banco de Horas'
Local cDesc2    := 'Ser impresso de acordo com os parametros solicitados pelo'
Local cDesc3    := 'usuario.'
Local cString	:= 'SRA' 	//-- Alias do arquivo principal (Base)
Local lEnd		:= .F.

Private aReturn  := { STR0001 , 1, STR0002 , 2, 2, 1, '',1 } //'Zebrado'###'Administrao'
Private nomeprog := 'PORX020'
Private aLinha   := {}
Private nLastKey := 0
Private cPerg    := 'PRX020'
Private Titulo   := OemToAnsi( 'Apurao Mensal de Banco de Horas')
Private cCabec	 := "MATRICULA NOME                                     PERIODO A COMPENSAR               TOTAL SALDO  HORAS COMPENSADAS"
Private AT_PRG   := 'PRX020'
Private CONTFL   := 1
Private nLin     := 80
Private nTamanho := 'M'
Private aInfo    := {}
Private wnrel
Private nOrdem
Private Limite	 := 132
Private m_pag := 1
//Ŀ
// Verifica as perguntas selecionadas                           
//
Pergunte( cPerg , .F. )

//Ŀ
// Envia controle para a funcao SETPRINT                        
//
wnrel := 'PORX020' //-- Nome Default do relatorio em Disco
wnrel := SetPrint(cString,wnrel,cPerg,@Titulo,cDesc1,cDesc2,cDesc3,.F.,aOrd,.T.,nTamanho)


//Ŀ
// Retorna a Ordem do Relatorio.                                
//
nOrdem    := aReturn[8]

//Ŀ
// Carregando variaveis mv_par?? para Variaveis do Sistema.     
//
Private FilialDe  := mv_par01	//Filial  De
Private FilialAte := mv_par02	//Filial  Ate
Private CcDe      := mv_par03	//Centro de Custo De
Private CcAte     := mv_par04	//Centro de Custo Ate
Private TurDe     := mv_par05	//Turno De
Private TurAte    := mv_par06	//Turno Ate
Private MatDe     := mv_par07	//Matricula De
Private MatAte    := mv_par08	//Matricula Ate
Private NomDe     := mv_par09	//Nome De
Private NomAte    := mv_par10	//Nome Ate
Private cData     := Substr(mv_par11,1,2)+"/"+Substr(mv_par11,3,4)	//Mes de Competencia - MM/AAAA
Private nMesAnt   := mv_par12	//Numero de meses anteriores (default 3)
Private cSit      := mv_par13	//Situacao
Private cCat      := mv_par14	//Categoria
//Private nCopias   := mv_par15	//Numero de Copias

IF !( nLastKey == 27 )
	
	SetDefault( aReturn , cString )
	
	IF !( nLastKey == 27 )
		
		RptStatus( { |lEnd| ImpExtraBh( @lEnd , wNRel , cString ) } , Titulo )
		
	EndIF
	
EndIF

Return()

Static Function ImpExtraBh( lEnd, wNRel, cString )

Local cAcessaSRA	:= &("{ || " + ChkRH("PORX020","SRA","2") + "}")
Local cFil       	:= ''
Local cMat       	:= ''
Local cTno       	:= ''
Local cFilTmp    	:= ''
Local cTnoAnt    	:= ''
Local lLoop      	:= .F.
Local nCount		:= 0
Local nSaldo		:= 0
Local nSaldoAnt		:= 0
Local aDet			:= {}
Local aDatas		:= {}
Local nI			:= 0

lEnd:=.F.

dbSelectArea('SRA')
dbSetOrder(nOrdem)
If nOrdem == 1
	dbSeek(FilialDe + CcDe + MatDe,.T.)
	cInicio  := 'RA_FILIAL + RA_CC + RA_MAT'
	cFim     := FilialAte + CcAte + MatAte
ElseIf nOrdem == 2
	dbSetOrder(8)
	dbSeek(FilialDe + CcDe + NomDe,.T.)
	cInicio  := 'RA_FILIAL + RA_CC + RA_NOME'
	cFim     := FilialAte + CcAte + NomAte
Endif

//Cabecalho
//MATRICULA NOME                                     PERIODO A COMPENSAR               TOTAL SALDO HORAS COMPENSADAS
//999999    1234567890123456789012345678901234567890 HORAS A COMPENSAR ATE DD/MM/AAAA  99999999.99 _________________
//1234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890
//         1         2         3         4         5         6         7         8         9        11

//-- Inicializa Rgua de Impresso
SetRegua(RecCount())

cCC    := ""

dbSelectArea("SRA")
While SRA->( !Eof() .and. &(cInicio) <= cFim )
	
	//-- Incrementa a Rgua de Impresso
	IncRegua()
		
	//-- Cancela a Impresso case se pressione Ctrl + A
	If lEnd
		IMPR(cCancela,'C')
		Exit
	EndIF
	
	//Ŀ
	// Consiste Parametrizacao do Intervalo de Impressao            
	//
	If (SRA->RA_TNOTRAB < Turde) .Or. (SRA->RA_TNOTRAB > TurAte) .Or. ;
		(SRA->Ra_NOME < NomDe) .Or. (SRA->RA_NOME > NomAte) .Or. ;
		(SRA->Ra_MAT < MatDe) .Or. (SRA->RA_MAT > MatAte)  .Or. ;
		(SRA->Ra_CC < CCDe) .Or. (SRA->RA_CC > CCAte)
		dbSkip()
		Loop
	Endif
	
	//-- Consiste Situacao e Categoria
	If !(Sra->Ra_SitFolh $ cSit) .Or. ;
		!(Sra->Ra_CatFunc $ cCat)
		DbSkip()
		Loop
	Endif
	
	//Ŀ
	// Consiste controle de acessos e filiais validas               
	//
	If !(SRA->RA_FILIAL $ fValidFil()) .Or. !Eval(cAcessaSRA)
		SRA->(dbSkip())
		Loop
	EndIf
		
	If SRA->RA_FILIAL #cFilTmp
		cFilTmp    := SRA->RA_Filial
		cTnoAnt    := ''
		//-- Atualiza o Array de Informaes sobre a Empresa.
		aInfo := {}
		fInfo(@aInfo, SRA->RA_Filial)
	Endif
	
	Set Device to Printer
	
	//-- Carrega os Totais de Horas e Abonos.
	aDet     :={}
	aDatas   :={}
	
	// Crio as datas equivalentes aos meses anteriores que devem ser listados
	// aDatas[x,1] -> Data Inicial
	// aDatas[x,2] -> Data Final
	// aDatas[x,3] -> Data Competencia para pagamento
	
	dData := cTod("30/"+cData)
	nMeses := 31 * nMesAnt
	For nI := 1 to nMesAnt
		AAdd(aDatas,{cTod("01/"+StrZero(Month(dData-nMeses),2)+"/"+Str(Year(dData-nMeses),4)),;
     				 LastDay(dData-nMeses),;
     				 cTod("30/"+StrZero(Month(dData),2)+"/"+Str(Year(dData),4))})
  		// Retorno data para dia 30, para que proxima subtracao caia novamente no
  		// mes anterior
     	dData := cTod("30/"+StrZero(Month(dData),2)+"/"+Str(Year(dData),4))
     	dData += 30 // subtraio 30 dias para carregar ms anterior
	Next nI
	
	For nI := 1 to Len(aDatas)
		dDtIni := aDatas[nI][1]
		dDtFim := aDatas[nI][2]
		dCompet:= aDatas[nI][3]
		nSaldo   :=0
						
		//Ŀ
		// Verifica lancamentos no Banco de Horas                       
		//
		dbSelectArea( "SPI" )
		dbSetOrder(2)
		If dbSeek( SRA->RA_FILIAL + SRA->RA_MAT )
			While !Eof() .And. SPI->PI_FILIAL+SPI->PI_MAT == SRA->RA_FILIAL+SRA->RA_MAT
			
				//-- Consiste Preenchimento de Cracha e data de Demisso
				If !Empty(SRA->RA_DEMISSA) .And. SRA->RA_DEMISSA < dDtIni
					SPI->(DbSkip())
					Loop
				Endif
			
		
				//Ŀ
				// Verifica os Lancamentos a imprimir                           
				//
				If	SPI->PI_DATA < dDtIni .Or. SPI->PI_DATA > dDtFim .Or. SPI->PI_STATUS == "B"
					SPI->(dbSkip())
					Loop
				Endif
				
				PosSP9( SPI->PI_PD ,SRA->RA_FILIAL )
				
				//Ŀ
				// Acumula os lancamentos de Proventos/Desconto em Array        
				//
				
				If SP9->P9_TIPOCOD $ "1*3"
					nSaldo:=SomaHoras(nSaldo,SPI->PI_QUANT)
				Else
					nSaldo:=SubHoras (nSaldo,SPI->PI_QUANT)
				Endif
				
				SPI->( dbSkip() )
			Enddo
			AAdd(aDet,{dCompet,nSaldo})
		EndIf
	Next nI
	If Len(aDet) > 0
	
		If nLin > 57
			nLin := Cabec(Titulo+" Compentncia: "+cData,cCabec,"",NomeProg,nTamanho,1)
		Endif
	
		If cCC != SRA->RA_CC
			If ! Empty(cCC)
				nLin := 60
				@ nLin,075 pSay Replicate("_",39)
				@ nLin,075 pSay "       Assinatura do Responsavel"
				nLin := Cabec(Titulo+" Compentncia: "+cData,cCabec,"",NomeProg,nTamanho,1)
			EndIf	
			@ ++nLin,000 pSay Trim(SRA->RA_CC) + " - " +;
				Posicione("CTT",1,xFilial("CTT")+SRA->RA_CC,"CTT_DESC01")
			nLin+=2
			cCC := SRA->RA_CC
		EndIf

		@ nLin,000 PSay SRA->RA_MAT
		@ nLin,010 pSay Substr(SRA->RA_NOME,1,40)
		For nI := 1 To Len(aDet)
			@ nLin,051 pSay "HORAS A COMPENSAR AT "+dToC(aDet[nI][1])
			@ nLin,085 pSay aDet[nI][2] Picture "99999999.99"
			@ nLin,098 pSay Replicate("_",17)
			nLin +=2
		Next nI
		nLin ++
		
	EndIf	
	
	dbSelectArea("SRA")
	dbSkip()
Enddo

@ ++nLin,045 pSay "_______________________________________"
@ ++nLin,045 pSay "       Assinatura do Responsavel"

//Ŀ
// Termino do relatorio                                         
//
dbSelectArea('SPI')
dbSetOrder(1)

dbSelectArea('SRA')
dbSetOrder(1)
Set Filter To
Set Device To Screen
If aReturn[5] == 1
	Set Printer To
	dbCommit()
	OurSpool(wnrel)
Endif
MS_FLUSH()

Return( )


/*


ͻ
Programa  Imprime   Autor  Ana Claudia          Data   17/11/06   
͹
Desc.     Impressao de dados referentes aos funcionrios que estiverem
          dentro dos parmetros informados                            
͹
Uso       PORX020                                                    
ͼ


*/

Static Function Imprime(aDet)

Return( )